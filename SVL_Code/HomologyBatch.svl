/**
 * Author: Christopher Sheaf
 * Last Modified: 4-9-2014
 *
 * Other code files this file is dependent on:
 *   'File_Operations.svl'
 *   'UI_Elements.svl'
 */

#set main 'HomologyBatch';

//Import outside functions:
function pro_HomologyModel;
function pro_Align;
function Chains;
function ReadAuto;
function GetFilesWithExtension;
function CreateErrorPrompt;
function CreateOptionsPrompt;

//Forward-declare local functions:
local function RunBatch;
local function LoadTemplate;
local function LoadSequences;
local function LoadModelingOptions;
local function CreateNewModelingOptions;

global function HomologyBatch [resourceDirectory]
	//Load all needed external code files:
	load 'File_Operations.svl';
	load 'UI_Elements.svl';

	//Search for and load all needed resources:
	if length resourceDirectory == 0 then //If a resource directory was not specified, look for the files in the current directory:
		resourceDirectory = '.';
	endif
	
	local system = SystemPush [];
	
	local templateFile = LoadTemplate [resourceDirectory];
	local sequenceFile = LoadSequences [resourceDirectory];
	ReadAuto templateFile;
	ReadAuto sequenceFile;
	
	//The template and sequence files must be read before calling LoadModelingOptions, in case the function tries to create new modeling options.
	//Modeling options cannot be created without an active template and sequence loaded.
	local modelingOptions = LoadModelingOptions [resourceDirectory];
	modelingOptions = modelingOptions(1)(1); //Strip away the outer vector, to get at the correct options.
	
	SystemPop system;
	
	RunBatch [modelingOptions, templateFile, sequenceFile];
endfunction

global function HomologyBatch_NoGUI [modelingOptions, templateFile, sequenceFile]
	local options = fread [modelingOptions, '{v:}'];
	options = options(1)(1); //Strip away the outer vector, to get at the correct options.
	
	RunBatch [options, templateFile, sequenceFile];
endfunction

/**
 * Searches for all necessary files and then starts the batch modeling job.
 */
local function RunBatch [modelingOptions, templateFile, sequenceFile]
	local i = 2; //Start at the first sequence (which is #2, since #1 is the template)
	loop
		//A new environment is created, and the files are re-read each time to avoid strange errors with MOE.
		local system = SystemPush [];
		
		ReadAuto templateFile;
		ReadAuto sequenceFile;
		local sequenceChains = Chains [];
		local ubound = length sequenceChains;
		print ubound;
		
		//Set the sequence to be modeled against the template, to each sequence in sequenceChains, one at a time:
		modelingOptions.model = i;
		
		//Name the new files the same as the chain that produced them
		modelingOptions.moe_file = token cat [string cName sequenceChains(i), ".moe"];
		modelingOptions.db_file = token cat [string cName sequenceChains(i), ".mdb"];
		modelingOptions.seq1 = cName sequenceChains(i);

        pro_Align [[sequenceChains(1), sequenceChains(i)], [verbose:1]];
		
		print token cat ["Running Modeling Job #", string totok i - 1];
		pro_HomologyModel [sequenceChains, modelingOptions];
		
		SystemPop system;
		
		i = i + 1;
	until i > ubound endloop
	print 'Finished.';
endfunction

/**
 * Searches for files with the extension .pdb in the specified directory.
 * If more than one is found, the user is prompted to select one from the list.
 */
local function LoadTemplate [directory]
	local pdbFiles = GetFilesWithExtension [directory, 'pdb'];
	local selectedFile = [];
	
	//Determine the correct file (if possible):
	if length pdbFiles == 1 then
		selectedFile = pdbFiles;
	elseif length pdbFiles > 1 then //We need a single .pdb File, so prompt the user if more than one is found:
		selectedFile = CreateOptionsPrompt [
			'Multiple .pdb files were found in this folder, but only one can be used as a template. Please select the .pdb file you would like to use as a template:',
			pdbFiles
		];
	else
		CreateErrorPrompt ['ERROR: No .pdb files were found to use as a template! Press \'Ok\' to exit.'];
	endif
	
	//Load the file and return its identifier:
	return selectedFile;
endfunction

/**
 * Searches for files with the extension .fst in the specified directory.
 * If more than one is found, the user is prompted to select one from the list.
 */
local function LoadSequences [directory]
	local fstFiles = GetFilesWithExtension [directory, 'fst'];
	local selectedFile = [];
	
	//Determine the correct file (if possible):
	if length fstFiles == 1 then
		selectedFile = fstFiles;
	elseif length fstFiles > 1 then //We need a single .fst File, so prompt the user if more than one is found:
		selectedFile = CreateOptionsPrompt [
			'Multiple .fst files were found in this folder, but only one can be used. Please select the .fst file that contains the needed sequences:',
			fstFiles
		];
	else
		CreateErrorPrompt ['ERROR: No .fst files were found in this folder! Press \'Ok\' to exit.'];
	endif
	
	//Load the file and return its identifier:
	return selectedFile;
endfunction

/**
 * Checks to see if a file named 'HomologyOptions.dat' exists. If it does, the options vector it contains is returned.
 *
 * If it doesn't, then a message informing the user that it does not exist will be displayed,
 * with the choice of whether or not to create a new one. If 'no' is chosen, the script exits.
 */
local function LoadModelingOptions [directory]
	local filePath = token cat [string directory, "/HomologyOptions.dat"];
	local fileHandle = _fopenr filePath; //Load HomologyOptions.dat from file path: 'directory'/HomologyOptions.dat
	local modelingOptions = [];
	
	if fileHandle == 0 then //If the file does not already exist:
		local prompt = WindowCreate [
			name: 'shell',
			title: 'Warning: Not Found',
			text: ['Yes, Create New', 'No, Exit Now'],
			onTrigger: ['return', 'exit'], bubbleHelp: ['Open the homology modeling options screen.', ''],
			
			Label: [
				text: 'Warning: No existing homology batch options found. Would you like to create a new file with these options?',
				margin: 1
			]
		];
		WindowShow prompt;
		WindowWait prompt;
		WindowDestroy prompt;

		modelingOptions = CreateNewModelingOptions [directory];
	else
		modelingOptions = fread [fileHandle, '{v:}'];
	endif
	
	return modelingOptions;
endfunction

local function CreateNewModelingOptions [directory]
	local [chains, options] = run ['$MOE/lib/svl/run/promodel_ui.svl', [], 'pro_Model_Prompt'];
	
	//Write options to a file named 'HomologyOptions.dat':
	local filePath = token cat [string directory, "/HomologyOptions.dat"];
	local fileHandle = _fopenw filePath;

	fwrite [fileHandle, '{v}', options];
	fclose fileHandle;
	
	return options;
endfunction
